# Copyright 2014 The Goma Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("base_config") {
  include_dirs = [ "." ]
}

static_library("base") {
  sources = [
    "basictypes.h",
    "compiler_specific.h",
    "file.cc",
    "file.h",
    "file_dir.cc",
    "file_dir.h",
    "join.cc",
    "join.h",
    "lockhelper.cc",
    "lockhelper.h",
    "path.cc",
    "path.h",
    "split.cc",
    "split.h",
    "string_piece_utils.cc",
    "string_piece_utils.h",
    "strutil.cc",
    "strutil.h",
  ]
  deps = [
    "//third_party:glog",
  ]
  public_deps = [
    "//third_party/chromium_base:platform_thread",
    "//third_party/chromium_base:string",
  ]

  if (os == "win") {
    sources += [
      "config_win.h",
      "socket_helper_win.cc",
      "socket_helper_win.h",
    ]

    cflags = [
      # These warnings come from int and SOCKET type mismatch.
      "/wd4309",  # Truncation of constant value
    ]
  }
  public_configs = [ ":base_config" ]
}

static_library("goma_unittest") {
  testonly = true
  sources = [
    "goma_unittest.cc",
  ]
  deps = [
    "//third_party:gtest",
  ]
}

executable("join_unittest") {
  testonly = true
  sources = [
    "join_unittest.cc",
  ]
  deps = [
    ":base",
    ":goma_unittest",
    "//build/config/sanitizers:deps",
    "//third_party:glog",
    "//third_party:gtest",
  ]
}

executable("lockhelper_unittest") {
  testonly = true
  sources = [
    "lockhelper_unittest.cc",
  ]
  deps = [
    ":base",
    ":goma_unittest",
    "//build/config/sanitizers:deps",
    "//third_party:glog",
    "//third_party:gtest",
  ]
}

executable("split_unittest") {
  testonly = true
  sources = [
    "split_unittest.cc",
  ]
  deps = [
    ":base",
    ":goma_unittest",
    "//build/config/sanitizers:deps",
    "//third_party:glog",
    "//third_party:gtest",
  ]
}

executable("string_piece_utils_unittest") {
  testonly = true
  sources = [
    "string_piece_utils_unittest.cc",
  ]
  deps = [
    ":base",
    ":goma_unittest",
    "//build/config/sanitizers:deps",
    "//third_party:glog",
    "//third_party:gtest",
  ]
}

executable("strutil_unittest") {
  testonly = true
  sources = [
    "strutil_unittest.cc",
  ]
  deps = [
    ":base",
    ":goma_unittest",
    "//build/config/sanitizers:deps",
    "//third_party:glog",
    "//third_party:gtest",
  ]
}

if (os == "win") {
  executable("socket_helper_win_unittest") {
    testonly = true
    sources = [
      "socket_helper_win_unittest.cc",
    ]
    deps = [
      ":base",
      ":goma_unittest",
      "//build/config/sanitizers:deps",
      "//third_party:glog",
      "//third_party:gtest",
    ]
  }
}
